apply plugin: "android-sdk-manager"
apply plugin: "com.android.$project.androidProjectType"
apply from: "$rootProject.projectDir/gradle/retrolambda.gradle"
apply from: "$rootProject.projectDir/gradle/android-apt.gradle"
apply from: "$rootProject.projectDir/gradle/android-unit-tests.gradle"

ext {
    isApplicationProject = (androidProjectType == 'application')
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }

    //noinspection GroovyAssignabilityCheck
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds true
        abortOnError false
    }

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled isApplicationProject
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (isApplicationProject) {
                signingConfig signingConfigs.release
            }
        }
    }
}

def getLocalProperties() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

repositories {
    maven {
        url "${getLocalProperties().getProperty('sdk.dir')}/extras/android/m2repository/"
    }
}
